{ lib, ... }:
{
  flake.modules.nixos.cloudflared =
    { pkgs, config, ... }:
    let
      cfg = config.my.cloudflared;
    in
    {
      options.my.cloudflared = {
        ingress = lib.mkOption {
          type = lib.types.attrs;
          default = { };
          description = "Ingress rules passed directly to services.cloudflared.tunnels.<id>.ingress";
        };
      };

      config = {
        clan.core.vars.generators.cloudflared-tunnel = {
          prompts.credentials-file = {
            description = "Creds file generated by cloudflare cli.";
            display.helperText = ''
              To generate, run:
                cloudflared tunnel login 
                cloudflared tunnel create <name> 
            '';
            type = "hidden";
            persist = true;
          };
          files.tunnel-id.secret = false;
          script = ''
            jq -r '.TunnelID' $prompts/credentials-file > $out/tunnel-id
            truncate -s -1 $out/tunnel-id
          '';
          runtimeInputs = with pkgs; [ jq ];
        };

        services.cloudflared = {
          enable = true;

          tunnels = {
            "${config.clan.core.vars.generators.cloudflared-tunnel.files.tunnel-id.value}" = {
              credentialsFile = config.clan.core.vars.generators.cloudflared-tunnel.files.credentials-file.path;

              default = "http_status:404";
              inherit (cfg) ingress;
            };
          };
        };
      };
    };
}
