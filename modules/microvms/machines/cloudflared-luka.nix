{ self, ... }:
{
  flake.clan.machines.vm-cloudflared-luka =
    { config, pkgs, ... }:
    {
      imports = with self.modules.nixos; [
        microvms_client
      ];

      clan.core.vars.generators = {
        cloudflared-tunnel = {
          prompts.credentials-file = {
            description = "Creds file generated by cloudflare cli.";
            display.helperText = ''
              To generate, run:
                cloudflared tunnel login
                cloudflared tunnel create <name>
            '';
            type = "hidden";
            persist = true;
          };
          files.tunnel-id.secret = false;
          script = ''
            jq -r '.TunnelID' $prompts/credentials-file > $out/tunnel-id
            truncate -s -1 $out/tunnel-id
          '';
          runtimeInputs = with pkgs; [ jq ];
          share = true;
        };
      };

      services.cloudflared = {
        enable = true;
        tunnels = {
          "${config.clan.core.vars.generators.cloudflared-tunnel.files.tunnel-id.value}" = {
            credentialsFile = config.clan.core.vars.generators.cloudflared-tunnel.files.credentials-file.path;
            default = "http_status:404";
            ingress = {
              "pocket.thekoppe.com" = {
                service = "https://10.0.0.5";
                originRequest.originServerName = "pocket.thekoppe.com";
              };
              "git.thekoppe.com" = {
                service = "https://10.0.0.8";
                originRequest.originServerName = "git.thekoppe.com";
              };
            };
          };
        };
      };
    };
}
