{
  config,
  lib,
  modulesPath,
  ...
}:
let
  importModule = path: import path { inherit config lib; };
in
{
  # see <https://github.com/nix-community/nixos-generators/blob/master/formats/qcow-efi.nix>

  options.myNixOS.profiles.proxmox-vm = {
    enable = lib.mkEnableOption "profile for proxmox LXC's";

    diskInitType = lib.mkOption {
      type = lib.types.nullOr (
        lib.types.enum [
          "disko"
          "qcow-efi"
        ]
      );
      default = "qcow-efi";
      description = "Method used to initialize disk image.";
    };
  };

  config =
    let
      cfg = config.myNixOS.profiles.proxmox-vm;
    in
    lib.mkIf cfg.enable (
      lib.mkMerge [
        {
          myNixOS.profiles.server.enable = true;

          services.qemuGuest.enable = true;
          networking.useDHCP = false; # override generated `true`

          boot.loader.timeout = lib.mkForce 5;
          boot.loader.grub = {
            enable = lib.mkDefault true;
            efiSupport = true;
            efiInstallAsRemovable = true;
          };
        }

        (importModule "${modulesPath}/profiles/qemu-guest.nix")
        (importModule "${modulesPath}/installer/scan/not-detected.nix")

        # originally generated by nixos-generate-config
        {
          boot = {
            initrd = {
              availableKernelModules = [
                "uhci_hcd"
                "ehci_pci"
                "ahci"
                "virtio_pci"
                "virtio_scsi"
                "sd_mod"
              ];
              kernelModules = [ ];
            };
            kernelModules = [ ];
            extraModulePackages = [ ];
          };

          networking.useDHCP = lib.mkDefault true;

          nixpkgs.hostPlatform = lib.mkDefault "x86_64-linux";
        }

        (lib.mkIf (cfg.diskInitType == "qcow-efi") {
          boot = {
            growPartition = true;
            kernelParams = map (c: "console=${c}") [ "ttyS0" ];
            loader.grub.device = "nodev";
          };

          fileSystems."/" = {
            device = "/dev/disk/by-label/nixos";
            autoResize = true;
            fsType = "ext4";
          };

          fileSystems."/boot" = {
            device = "/dev/disk/by-label/ESP";
            fsType = "vfat";
          };
        })
      ]
    );
}
